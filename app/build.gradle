apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "sean.com.gankio"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true // 突破应用方法书65535的限制
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    /**
     * 配置签名信息
     */
    signingConfigs {
        release {
            storeFile file("${releaseSignStorePath}")
            storePassword "${releaseSignStorePass}"
            keyAlias "${releaseSignAlias}"
            keyPassword "${releaseSignAliasPass}"
        }
        debug {
            storeFile file("${debugSignStorePath}")
            storePassword "${debugSignStorePass}"
            keyAlias "${debugSignAlias}"
            keyPassword "${debugSignAliasPass}"
        }
    }

    /**
     * 配置编译信息
     */
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            //指定release包的输出文件名就是我们渠道名字
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith(".apk")) {
                        def fileName = "${variant.productFlavors[0].name}" + ".apk"
                        output.outputFile = new File(outputFile.parent, fileName);
                    }
                }
            }
        }
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    /**
     * 配置不同包名，可以安装两个版本的app
     */
    productFlavors {
        beta {
            applicationId 'sean.com.gankio.beta'
        }
        production {
            applicationId 'sean.com.gankio'
        }

        xiaomi {
            //manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
            // 指定app的名称，String 代表类型，app_name代表strings.xml中的key，
            // xiaomi_app代表strings.xml中的对应key的value
            resValue "string", "app_name", "xiaomi_app"
        }
        wandoujia {
            //manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
            resValue "string", "app_name", "wandoujia_app"
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: "name"]
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    /** Glide加载图片*/
    /** view的注入框架*/
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:support-v4:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.jakewharton:butterknife:8.5.1'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.zhj:syringe:1.0.0'
    compile 'com.android.support.test.espresso:espresso-core:2.2.2'
    compile 'com.alibaba:fastjson:1.2.28'
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile 'com.wang.avi:library:2.1.3'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    annotationProcessor 'com.zhj:syringe-compiler:1.0.0'
    compile 'com.wang.avi:library:2.1.3'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.2'
    compile files('libs/BmobStat_v1.1_170114.jar')
    compile 'com.umeng.analytics:analytics:latest.integration'
}
