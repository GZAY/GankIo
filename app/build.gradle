apply plugin: 'com.android.application'

//测试版本号
def debugVersionCode = 1
//测试版本名
def debugVersionName = '1.0'

//生产版本号
def releaseVersionCode = 1
//生产版本名
def releaseVersionName = '1.0'


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "sean.com.gankio"
        minSdkVersion 15
        targetSdkVersion 25
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true // 突破应用方法书65535的限制
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    /**
     * 配置签名信息
     */
    signingConfigs {
        release {
            storeFile file("${releaseSignStorePath}")
            storePassword "${releaseSignStorePass}"
            keyAlias "${releaseSignAlias}"
            keyPassword "${releaseSignAliasPass}"
        }
        debug {
            storeFile file("${debugSignStorePath}")
            storePassword "${debugSignStorePass}"
            keyAlias "${debugSignAlias}"
            keyPassword "${debugSignAliasPass}"
        }
    }

    /**
     * 配置编译信息
     */
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'//包名之后加上.debug 为了同时安装debug和release两个版本
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    /**
     * 配置不同包名，可以安装两个版本的app
     */
    productFlavors {
        xiaomi {
            //manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
            // 指定app的名称，String 代表类型，app_name代表strings.xml中的key，
            // xiaomi_app代表strings.xml中的对应key的value
            resValue "string", "app_name", "干货-小米"
        }
        wandoujia {
            //manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
            resValue "string", "app_name", "干货-豌豆荚"
        }
    }

    /**
     * 根据不同的Flavors名字来设置友盟渠道
     */
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: "name"]
    }

    /**
     * 配置版本、版本号，以及生成的apk名称
     */
    applicationVariants.all { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.mergedFlavor.versionCode = releaseVersionCode
            variant.mergedFlavor.versionName = releaseVersionName
        } else if (variant.buildType.name.equals('debug')) {
            variant.mergedFlavor.versionCode = debugVersionCode
            variant.mergedFlavor.versionName = debugVersionName
        }
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith(".apk")) {
                def fileName = "${variant.productFlavors[0].name}" + "-" + new Date().format('yyyy-MM-dd') + "-" + variant.buildType.name + ".apk"
                output.outputFile = new File(outputFile.parent, fileName);
            }
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    /** Glide加载图片*/
    /** view的注入框架*/
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:support-v4:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'jp.wasabeef:glide-transformations:2.0.2'
    compile 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1'
    compile 'com.jakewharton:butterknife:8.5.1'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.zhj:syringe:1.0.0'
    compile 'com.android.support.test.espresso:espresso-core:2.2.2'
    compile 'com.alibaba:fastjson:1.2.28'
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile 'com.wang.avi:library:2.1.3'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    annotationProcessor 'com.zhj:syringe-compiler:1.0.0'
    compile 'com.wang.avi:library:2.1.3'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.2'
    compile files('libs/BmobStat_v1.1_170114.jar')
    compile 'com.umeng.analytics:analytics:latest.integration'
}
